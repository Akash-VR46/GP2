Main = if Series-parallel then yes else no
Series-parallel = Reduce!; delete-base; if nonempty then fail
Reduce = {par, seq}

par (a, b, x, y: list) [
(n1, x) (n2, y) | (e1, n1, n2, a) (e1, n1, n2, b) 
] => [
(n1, x) (n2, y) | (e1, n1, n2, empty) 
]
interface = {n1, n2}


seq (a,b,x,y,z: list) [
(n1, x) (n2, y) (n3, z) | (e1, n1, n2, a) (e2, n2, n3, b)
] => [
(n1, x) (n3, z) | (e1, n1, n3, empty) 
]
interface = {n1, n3}


delete-base (a, x, y: list) [
(n1, x) (n2, y) | (e1, n1, n2, a) 
] =>
[  |  ]
interface = {}


nonempty (x: list) [ (n1, x) | ] => [ (n1, x) | ]
interface = {n1}


yes () [ | ]
=> [ (n1, "yes" # grey) | ]
interface = {}

no () [ | ]
=> [ (n1, "no" # grey) | ]
interface = {}
